" vimrc
" =====
"
" table of contents
" -----------------
"   * plugins
"   * sets
"   * colors
"   * plugin options
"   * functions
"   * remaps
"   * auto commands

set nocompatible

" plugins
" -------
set nocompatible
filetype off

call plug#begin()
  " core
  Plug 'junegunn/vim-plug'
  Plug 'mkarmona/colorsbox'          " color scheme
  " Plug 'davidhalter/jedi-vim'        " python completion

  " syntax
  Plug 'vim-scripts/AnsiEsc.vim'     " escape ANSI sequences
  " Plug 'Glench/Vim-Jinja2-Syntax'    " jinja html templates
  " Plug 'leafgarland/typescript-vim'  " typescript
  Plug 'vim-scripts/nginx.vim'       " nginx
  " Plug 'chikamichi/mediawiki.vim'    " wikipedia source
  Plug 'digitaltoad/vim-pug'         " pug
  " Plug 'isRuslan/vim-es6'
  " Plug 'othree/yajs.vim'
  Plug 'gisphm/vim-gitignore'        " .gitignore files
  Plug 'alvan/vim-closetag'
  Plug 'martinda/Jenkinsfile-vim-syntax'
  Plug 'vim-ruby/vim-ruby'

  " misc
  Plug 'vim-utils/vim-man'           " `Man` command
  Plug 'jiangmiao/auto-pairs'        " completes pairs
  Plug 'ctrlpvim/ctrlp.vim'          " fuzzy file finder
  Plug 'scrooloose/nerdtree'         " filetree
  Plug 'ryanoasis/vim-devicons'
  Plug 'tiagofumo/vim-nerdtree-syntax-highlight'
  " Plug 'ervandew/supertab'
  " Plug 'scrooloose/syntastic'        " syntax linter
  " Plug 'joonty/vim-do.git'
  " Plug 'maneyko/vim-do'              " execute shell commands

  " filetype specific
  " Plug 'mattn/emmet-vim'             " html completion

  Plug 'neoclide/coc.nvim', {'branch': 'release', 'do': { -> coc#util#install()}}
  Plug 'autozimu/LanguageClient-neovim', {
      \ 'branch': 'next',
      \ 'do': 'bash install.sh',
      \ }


  " tpope
  Plug 'tpope/vim-commentary'        " commenting motions
  Plug 'tpope/vim-endwise'           " closes functions (if and fi)
  Plug 'tpope/vim-fugitive'          " git integration
  Plug 'tpope/vim-rails'
  Plug 'tpope/vim-repeat'            " '.' for plugins
  Plug 'tpope/vim-surround'          " surrounding motions
  Plug 'tpope/vim-unimpaired'        " bracket functions and more

call plug#end()
filetype plugin indent on

" sets
" ----
set autoindent
if has('patch-7.4-346')
  set breakindent
endif
set clipboard=unnamed
set encoding=UTF-8
set expandtab
set hlsearch
set ignorecase
set incsearch
set linebreak
set list
set listchars=tab:\ \ ,trail:â‹…
if has('mouse')
  set mouse=a
endif
if version >= 702
  au InsertLeave * set list
  au InsertEnter * set nolist
endif
set number
set numberwidth=3
set ruler
set scrolloff=1
set shiftwidth=4
set showcmd
set smartcase
set smartindent
set smarttab
set softtabstop=4
set t_Co=256
set tabstop=4
set tildeop
set nowrap

let g:netrw_dirhistmax=0  " no .netrwhist files


" colors
" ------
if !empty(glob($VIMRUNTIME . '/syntax/syntax.vim'))
  syntax on
endif
if isdirectory(glob('~/.vim/plugged/colorsbox'))
  colorscheme colorsbox-stnight
endif

" make bg and fg colors same as terminal
highlight Normal ctermbg=NONE
highlight Normal ctermfg=NONE


" plugin options
" --------------
let g:user_emmet_leader_key = '<NUL>'  " equals '<C-space>,'
let g:do_refresh_key = '<C-M>'
" let g:SuperTabCrMapping = 1
" let g:SuperTabDefaultCompletionType = "<c-n>"
let g:vimpager = {}
let g:less = {}
let g:less.enabled = 0
let g:less.scrolloff = 5
let NERDTreeShowHidden=1
autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif
" let NERDTreeMapOpenInTab='<ENTER>'

let g:ctrlp_prompt_mappings = {
    \ 'AcceptSelection("e")': ['<2-LeftMouse>'],
    \ 'AcceptSelection("t")': ['<cr>'],
\ }
let g:ctrlp_regexp = 1

inoremap <silent><expr> <TAB>
      \ pumvisible() ? "\<C-n>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

" let g:coc_global_extensions = ['coc-solargraph']


" functions
" ---------
fun! ReadMode(readmode_togg)
  " acts like ``less``
  if a:readmode_togg==1
    silent! call FlyMode(0)
    silent! setlocal nomodifiable
    silent! setlocal readonly
    silent! setlocal nolist
    silent! setlocal timeout timeoutlen=0 ttimeoutlen=0
    silent! nnoremap <buffer> r :call ReadMode(0)<CR>
    silent! nnoremap <buffer> q :q<CR>
    silent! nnoremap <buffer> x :q!<CR>
    silent! nnoremap <buffer> j <C-e>L0:file<CR>
    silent! nnoremap <buffer> k <C-y>L0:file<CR>
    silent! nnoremap <buffer> d <C-d>L0:file<CR>
    silent! nnoremap <buffer> u <C-u>L0:file<CR>
    silent! nnoremap <buffer> o 5<C-e>L0:file<CR>
    silent! nnoremap <buffer> p 5<C-y>L0:file<CR>
    if @% == ''
      silent! nnoremap <buffer> q :q!<CR>
    endif
    let g:readmode_togg=0
    echo 'Read Mode on'
  else
    silent! setlocal modifiable
    silent! setlocal noreadonly
    silent! setlocal timeout nottimeout timeoutlen=1000 ttimeoutlen=-1
    silent! nunmap <buffer> r
    silent! nunmap <buffer> q
    silent! nunmap <buffer> x
    silent! nunmap <buffer> j
    silent! nunmap <buffer> k
    silent! nunmap <buffer> u
    silent! nunmap <buffer> d
    silent! nunmap <buffer> o
    silent! nunmap <buffer> p
    silent! execute 'normal! M'
    let g:readmode_togg=1
    echo 'Read Mode off'
  endif
endfun
let g:readmode_togg=1

fun! FlyMode(flymode_togg)
  " main function is setting so=999
  if a:flymode_togg==1
    silent! call ReadMode(0)
    silent! setlocal so=999
    " normal zz
    let g:flymode_togg=0
    echo 'Fly Mode on'
  else
    silent! setlocal so=1
    let g:flymode_togg=1
    echo 'Fly Mode off'
  endif
endfun
let g:flymode_togg=1

fun! TabFun(tb, expd)
  " retabs file
  execute 'setlocal tabstop='     . &l:tabstop
  execute 'setlocal softtabstop=' . &l:softtabstop
  execute 'setlocal shiftwidth='  . &l:shiftwidth
  setlocal noexpandtab
  %retab!
  if a:expd==1
    setlocal expandtab
    echo 'expandtab'
  else
    setlocal noexpandtab
    echo 'noexpandtab'
  endif
  execute 'setlocal tabstop='     . a:tb
  execute 'setlocal softtabstop=' . a:tb
  execute 'setlocal shiftwidth='  . a:tb
  %retab!
endfun

fun! AlignRow(tw_)
  for i in range(1, a:tw_)
    execute "normal! 080lF\<space>r\<cr>"
  endfor
endfun


" remaps
" ------
let g:mapleader = ','

nnoremap <C-h>            <C-w>h
nnoremap <C-j>            <C-w>j
nnoremap <C-k>            <C-w>k
nnoremap <C-l>            <C-w>l
nnoremap <C-i>            gT
nnoremap <C-o>            gt
nnoremap <C-b>            :nohl<CR><C-l>
nnoremap <C-n>            :set relativenumber!<CR>
nnoremap <leader><leader> :w<CR>
nnoremap <leader>f        :call FlyMode(flymode_togg)<CR>
nnoremap <leader>kq       :q!<CR>
nnoremap <leader>n        :NERDTreeToggle<CR>
nnoremap <leader>q        :q<CR>
nnoremap <leader>r        :call ReadMode(readmode_togg)<CR>
nnoremap <leader>s        :setlocal spell! spelllang=en_us<CR>
nnoremap <leader>kw       :w !sudo tee %<CR>
nnoremap \a               :<C-U>call AlignRow(v:count1)<CR>
nnoremap \e               :<C-U>call TabFun(v:count1*2,1)<CR>
nnoremap \n               :<C-U>call TabFun(v:count1*2,0)<CR>
nnoremap \r               :call ReadMode(readmode_togg)<CR>
nnoremap \s               1z=
nnoremap \w               :%s/\s\+$//<CR>:nohl<CR><C-b>
nnoremap Q                <Esc>
nnoremap K                :call CocActionAsync('jumpDefinition', 'tab drop')<CR>
nnoremap U                <C-r>
nnoremap Y                y$
nnoremap daf              :1,$d<CR>
nnoremap yaf              :1,$y<CR>
nnoremap zfs              :mkview<CR>
nnoremap zfl              :loadview<CR>


inoremap <C-a>            <C-o>^
inoremap <C-d>            <C-o>x
inoremap <C-e>            <C-o>$
inoremap <C-p>            <C-r>*
inoremap <C-w>            <C-o><C-w>
inoremap <M-b>            <C-o>dd
inoremap <M-f>            <C-o>w
inoremap jk               <Esc>

vnoremap .                :normal! .<CR>
vnoremap &                :normal! &<CR>

map <ScrollWheelUp>       <C-y>
map <ScrollWheelDown>     <C-e>


" auto commands
" -------------
au BufLeave *__doc__*,help
      \ silent! call ReadMode(0)
au BufNewFile *__doc__*
      \ silent! call ReadMode(1)
au BufNewFile *.sh,*.bash
      \ exe "normal! i#!/bin/bash\<CR>\<Esc>"
au BufNewFile *.node
      \ exe "normal! i#!/usr/bin/env node\<CR>\<Esc>"
au BufNewFile *.py
      \ exe "normal! i#!/usr/bin/env python\<CR>\<Esc>"
au BufNewFile *.pl
      \ exe "normal! i#!/usr/bin/env perl\<CR>\<Esc>x"
au BufNewFile *.html,*.php
      \ exe "normal!"
      \ . "i<!DOCTYPE html>\<CR>"
      \ . "<html>\<CR>"
      \ . "<head>\<CR>"
      \ . "<meta name=\"viewport\" content=\"width=device-width, initial-scale=1\">\<CR>"
      \ . "<meta charset=\"utf-8\">\<CR>"
      \ . "<title></title>\<CR>"
      \ . "</head>\<CR>"
      \ . "<body>\<CR>\<CR>"
      \ . "</body>\<CR>"
      \ . "</html>\<Esc>"
      \ . "/title\<CR>f<\<C-b>"

au BufRead *.ipynb
      \ setlocal ft=json
au BufRead *.url
      \ setlocal ft=dosini
au BufRead,BufNewFile
      \ /usr/local/etc/nginx/*,
      \/usr/local/nginx/conf/*,
      \/etc/nginx/*
      \ setlocal ft=nginx

au VimEnter *.rb,Rakefile NERDTree | wincmd w
" au BufEnter *.rb,Rakefile exe "silent! CocDisable"
" Configure ruby omni-completion to use the language client:
autocmd FileType ruby,eruby let g:rubycomplete_buffer_loading = 1 
autocmd FileType ruby,eruby let g:rubycomplete_classes_in_global = 1
autocmd FileType ruby,eruby let g:rubycomplete_rails = 1
" au FileType ruby setlocal omnifunc=LanguageClient#complete
au FileType css,html,javascript,
      \jinja,json,php,pug,R,rst,
      \sh,sql,tex,typescript,vim,yaml
      \ setlocal ts=2 sw=2 sts=2
au FileType c,cpp,php
      \ setlocal commentstring=//\ %s
au FileType help
      \ silent! call ReadMode(1)
au FileType php
      \ setlocal ft=html syn=php
au FileType tex
      \ setlocal spell spelllang=en_us colorcolumn=80 wrap foldlevel=10
au FileType tex,cpp
      \ nnoremap <leader>c :silent exec "!echo >> /tmp/_listener"<CR><C-b>
au FileType man
      \ setlocal so=0
au FileType vim
      \ setlocal keywordprg=:help
au FileType jinja
      \ setlocal commentstring=<!--%s-->
au FileType apache,nginx
      \ setlocal commentstring=#\ %s
au FileType sql
      \ setlocal commentstring=--%s
au FileType haml
      \ setlocal wrap

" vim: ts=2 sw=2 sts=2 viewoptions-=options
